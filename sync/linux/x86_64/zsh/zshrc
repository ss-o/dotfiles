# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# ⸨ Zsh ⸩
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

(){ source "${HOME}/.config/zi/init.zsh"; zzinit; }

# ⸨ History ⸩
COMPLETION_WAITING_DOTS=1
HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE=1

: ${HISTFILE:=${ZI[CACHE_DIR]}/.history}
[[ -e $HISTFILE ]] || { command mkdir -p ${HISTFILE:h}; command touch $HISTFILE; }
[[ -w $HISTFILE ]] && typeset -gx SAVEHIST=440000 HISTSIZE=441000 HISTFILE

# ⸨ LS COLORS ⸩
(( $+commands[vivid] )) && { typeset -gx LS_COLORS="$(vivid generate molokai)" 2> /dev/null; }

# ⸨ Dotfiles ⸩
typeset -gA DOTFILES
DOTFILES[ZSH]="${HOME}/.dotfiles/sync/linux/${CPUTYPE}/zsh"
DOTFILES[ZSH_SNIPPETS]="${DOTFILES[ZSH]}/snippets"
DOTFILES[ZSH_FUNCTIONS]="${DOTFILES[ZSH]}/functions"
DOTFILES[ZSH_COMPLETIONS]="${DOTFILES[ZSH]}/completions"

# ⸨ Functions ⸩
if [[ -z ${fpath[(re)${DOTFILES[ZSH_FUNCTIONS]}]} ]]; then
  typeset -gxU fpath FPATH
  fpath=( "${DOTFILES[ZSH_FUNCTIONS]}" "${fpath[@]}" )
fi

# ⸨ Initiate ⸩
@init(){
  setopt typeset_silent; typeset _init_1 _init_2 _init_3 _init_4
  zstyle ':zi:plugins:ssh-agent' quiet yes
  for _init_1 in ${DOTFILES[ZSH_FUNCTIONS]}/*; do autoload -Uz "${_init_1:t}"; done
  for _init_2 in ${DOTFILES[ZSH_COMPLETIONS]}/*; do ln -sf "${_init_2}" "${HOME}/.zi/completions/"; done
  for _init_3 in ${DOTFILES[ZSH_SNIPPETS]}/loaded/*.zsh; do source "$_init_3"; done
  for _init_4 in ${DOTFILES[ZSH]}/aliases/user/*.zsh; do source "$_init_4"; done
}; @init; unset -f @init

typeset -gx EDITOR=$(set-cmd vim micro nano)

# DEVOPS ============================================================================================================ #
zi is-snippet for \
  id-as'gcloud-completion' \
  if'[[ -d ~/.local/share/google-cloud-sdk/bin \
    && -f ~/.local/share/google-cloud-sdk/completion.zsh.inc ]]' \
  atinit'[[ -n ${path[(re)${HOME}/.local/share/google-cloud-sdk/bin]} ]] \
    || builtin source ~/.local/share/google-cloud-sdk/path.zsh.inc' \
  ~/.local/share/google-cloud-sdk/completion.zsh.inc

#zi has'svn' blockf for svn \
#  id-as'user:aliases' multisrc'{general,systemd/systemd}.zsh' pick'/dev/null' \
#    https://github.com/ss-o/dotfiles/trunk/sync/linux/x86_64/zsh/aliases/user
# ANNEX ============================================================================================================== #
zi light-mode compile'functions/.*za-*~*.zwc' for z-shell/z-a-meta-plugins @annexes
# META-PLUGINS ======================================================================================================= #
zi lucid for light-mode \
  skip'ripgrep peco tig bat exa hyperfine fd' @console-tools \
  skip'dircolors-materia' @console-style \
  skip'F-Sy-H' @z-shell @romkatv \
  skip'fzf fzy peco skim' @fuzzy \
  @zsh-users+fast
# OH-MY-ZSH PLUGINS ================================================================================================== #
zi-turbo '0a' light-mode for \
    atload"unalias grv g" OMZP::git \
    if'[[ -d ~/.gnupg ]]' OMZP::gpg-agent
# PLUGINS ============================================================================================================ #
zi-turbo '0a' for \
  binary sbin'bin/*' \
    z-shell/nb \
  binary from"gh-r" sbin \
    ajeetdsouza/zoxide \
  has'zoxide' \
    z-shell/zsh-zoxide \
  has'lsd' atinit'AUTOCD=1' \
    z-shell/zsh-lsd \
  atinit'YSU_MESSAGE_POSITION=after' \
    MichaelAquilina/zsh-you-should-use

[[ -r ${HOME}/.zshrc_local ]] && source ${HOME}/.zshrc_local
[[ -f ${DOTFILES[ZSH]}/themes/p10k-classic.zsh ]] && \
  { source ${DOTFILES[ZSH]}/themes/p10k-classic.zsh || return 1; }

#[[ -f ${DOTFILES[ZSH]}/themes/p10k-robbyrussell.zsh ]] && \
#  { source ${DOTFILES[ZSH]}/themes/p10k-robbyrussell.zsh || return 1; }
