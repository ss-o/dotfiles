# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Description: This file is sourced on all invocations of the shell.
# It is the 1st file zsh reads; it's read for every shell, even if started with
# -f (setopt NO_RCS), all other initialization files are skipped.
#
# This file should contain commands to set the command search path, plus other
# important environment variables. This file should not contain commands that produce
# output or assume the shell is attached to a tty.
#
# Loading order: zshenv, zprofile, zshrc, zlogin.

typeset -ghA DOTFILES

DOTFILES[CONFIG]=${HOME}/.dotfiles/.config

for environment in ${DOTFILES[CONFIG]}/env/*.sh; do
  source "$environment"
done

typeset -a create_path dir_path

create_path=(
  "${HOME}/.local/bin"
)

for dir_path in "${create_path[@]}"; do
  if [[ ! -d $dir_path ]]; then
    mkdir -p $dir_path 2> /dev/null
  fi
done; unset dir_path

prepend_path() {
  typeset -gxU path PATH
  for add in "$@"; do
    if [[ -d $add ]]; then
      path=( "${add}" "${(@)path}" )
    else
      path=( "${(@)path:#$add}" )
    fi
  done; unset -v add
  path=( "${(@u)path}" )
}

prepend_path "${create_path[@]}"

unset -f prepend_path

[[ -r ${HOME}/.env_local ]] && source ${HOME}/.env_local
