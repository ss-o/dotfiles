# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Description: Ping Test to determine Optimal MTU Size

builtin emulate -L zsh
builtin setopt extended_glob warn_create_global

local -a deps
local dep target=$1; shift
local lower=0 upper=1500

deps=( 'ping' )

for dep in "${deps[@]}"; do
  if (( ! $+commands[$dep] )); then
    print -Pr "Missing dependency: $dep"; return 100
  fi
done

if [[ -z $target ]]; then
  print "Missing target to check"; return 1
else
  until [[ $((lower + 1)) -eq $upper ]]; do
    current=$(((lower + upper) / 2))
    print -n "lower: $lower, upper: $upper, testing: $current -- "
    if command ping -M do -s $current -c 2 -i 0.2 $target $@ &>/dev/null; then
      print "ok"; lower=$current
    else
      print "fail"; upper=$current
    fi
  done
  print "max packet size: $lower, mtu: $((lower + 28))"; return 0
fi

