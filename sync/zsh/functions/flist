# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Description: List available functions.

builtin setopt local_options extended_glob

local -a pfxs lines desc files
pfxs=( zsh user system )                                                # grouped commands
pfxs+=( "*~$HOME/.dotfiles/sync/zsh/functions/(${(j:|:)pfxs}|lps)" )    # ungrouped commands
pfxs+=( local )                                                         # local, uncommited commands

integer pass longest=0 longest2=0
local p file header="# Description: "
for (( pass = 1; pass <= 2; ++ pass )); do
    for p in "${pfxs[@]}"; do
        files=( $HOME/.dotfiles/sync/zsh/functions/${~p}*(N) )
        if (( pass == 2 )); then
            if (( ${#files[@]} )); then
                if [[ "$p" = "lps" ]]; then
                    print -r -- ${(l:(longest+longest2)/2-4::-:):-} LOCAL ${(l:longest+longest2-(longest+longest2)/2-3::-:):-}
                else
                    print -r -- ${(l:longest+longest2::-:):-}
                fi
            fi
        fi
        for file in "${files[@]}"; do
            lines=( ${(f)"$(<$file)"} )
            desc=( "${(M)lines[@]:#${header}*}" )
            desc[1]="${desc[1]#$header}"
            file="${file:t}"
            [[ "$file" = (README.md) ]] && continue
            if (( pass == 1 )); then
                (( longest < ${#file} + 3 )) && longest=$(( ${#file} + 3 ))
                (( longest2 < ${#desc[1]} )) && longest2=$(( ${#desc[1]} ))
            else
                echo "${file}${(l:longest-${#file}:: :):- }${desc[1]}"
            fi
        done
    done
    if (( pass == 2 )); then
      print -r -- ${(l:longest+longest2::-:):-}
    fi
done
