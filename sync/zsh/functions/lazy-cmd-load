# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et

  local seperator='--'
  local seperator_index=${@[(ie)$seperator]}
  local cmd_list=(${@:1:(($seperator_index - 1))});
  local load_cmd=${@[(($seperator_index + 1))]};

  if [[ ! $load_cmd ]]
  then
    >&2 print "No load command defined"
    >&2 print "  $@"
    return 1
  fi

  if (( ${cmd_list[(I)${funcstack[2]}]} ))
  then
    unfunction $cmd_list
    eval "$load_cmd"
  else
    # create placeholder function for each command
    # ${(qqqq)VAR} will quote VAR value as $'...'
    local cmd
    for cmd in $cmd_list
    do
      eval "function $cmd {
        lazy-cmd-load $cmd_list $seperator ${(qqqq)load_cmd}
        $cmd \"\$@\"
      }"
    done
  fi
