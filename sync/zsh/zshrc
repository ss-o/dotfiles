
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

[[ -r "${XDG_CONFIG_HOME:-$HOME/.config}/zi/init.zsh" ]] && {
  source "${XDG_CONFIG_HOME:-$HOME/.config}/zi/init.zsh" && zzinit
} || { 
  source <(curl -sL init.zshell.dev); zzinit;
}

# ‑‑‑‑‑‑ ⸨ Global Environment ⸩ ‑‑‑‑‑‑

typeset -gx SAVEHIST=500000 HISTSIZE=501000 HISTFILE="${ZI[CACHE_DIR]}/.history"

# ‑‑‑‑‑‑ ⸨ Zsh Environment ⸩ ‑‑‑‑‑‑

COMPLETION_WAITING_DOTS=true
HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE=1

[[ $COLORTERM = *(24bit|truecolor)* ]] || { zmodload zsh/nearcolor 2> /dev/null; }
(( $+commands[vivid] )) && { export LS_COLORS="$(vivid generate molokai)" 2> /dev/null; }

# ‑‑‑‑‑‑ ⸨ Zsh Options ⸩ ‑‑‑‑‑‑

setopt always_to_end            # when completing from the middle of a word, move the cursor to the end of the words
setopt append_history       	  # allow multiple terminal sessions to all append to one zsh command history
setopt auto_cd                  # cd by typing the directory name if it's not a command
setopt auto_list                # automatically lists choices on an ambiguous completion
setopt auto_menu                # automatically use menu completion
setopt auto_pushd               # make cd push the old directory onto the directory stack
setopt bang_hist                # treat the '!' character, especially during expansion.
setopt extended_glob            # treat #, ~, and ^ as part of patterns for filename generation
setopt extended_history         # record timestamp of command in HISTFILE
setopt glob_dots                # show dotfiles in completions
setopt hist_expire_dups_first   # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_all_dups     # remove older duplicate entries from the history
setopt hist_ignore_dups         # ignore duplicated commands history list
setopt hist_ignore_space        # ignore commands that start with space
setopt hist_reduce_blanks       # remove superfluous blanks from history items
setopt hist_save_no_dups        # do not write A duplicate event to the history file
setopt hist_verify              # show command with history expansion to the user before running it
setopt inc_append_history       # write to the history file immediately, not when the shell exits.
setopt interactive_comments     # allow comments even in interactive shells
setopt multios                  # perform implicit tees or cats when multiple redirections are attempted
setopt no_beep                  # don't beep on error
setopt prompt_subst             # enable parameter expansion, command substitution, and arithmetic expansion in the prompt
setopt pushd_ignore_dups        # don't push multiple copies of the same directory onto the directory stack
setopt share_history            # share command history data
unsetopt rm_star_silent         # ask for confirmation for rm * and rm path/

# ‑‑‑‑‑‑ ⸨ Private functions ⸩ ‑‑‑‑‑‑
if [[ -z ${fpath[(re)${HOME}/.dotfiles/sync/zsh/functions]} ]]; then
  typeset -gxU fpath FPATH
  fpath=( "${HOME}/.dotfiles/sync/zsh/functions" "${fpath[@]}" )
fi

for fnc in ${HOME}/.dotfiles/sync/zsh/functions/*; do
  builtin autoload -Uz $fnc
done
for sni in ${HOME}/.dotfiles/sync/zsh/snippets/*; do
  builtin source $sni
done
unset sni fnc

typeset -gx EDITOR=$(set-cmd vim micro nano)
# DEVOPS ============================================================================================================ #
if [[ -z ${path[(re)/opt/google-cloud-sdk/bin]} ]] && [[ -d /opt/google-cloud-sdk/bin ]]; then
  source /opt/google-cloud-sdk/path.zsh.inc
fi
zi ice id-as'gcloud-completion'
zi snippet /opt/google-cloud-sdk/completion.zsh.inc

zi has'svn' blockf for svn \
  id-as'user:aliases' multisrc'{general,systemd/systemd}.zsh' pick'/dev/null' \
    https://github.com/ss-o/dotfiles/trunk/sync/zsh/aliases/user
# ANNEX ============================================================================================================== #
zi light-mode compile'functions/.*za-*~*.zwc' for z-shell/z-a-meta-plugins @annexes
# META-PLUGINS ======================================================================================================= #
zi lucid for light-mode \
  skip'ripgrep peco tig bat hyperfine fd' @console-tools \
  skip'dircolors-materia' @console-style \
  skip'F-Sy-H' @z-shell @romkatv \
  skip'fzf fzy peco skim' @fuzzy \
  @zsh-users+fast
# OH-MY-ZSH PLUGINS ================================================================================================== #
zi-turbo '0a' light-mode for \
  atload"unalias grv g" OMZP::git \
    if'[[ -d ~/.gnupg ]]' OMZP::gpg-agent \
    if'[[ -d ~/.ssh ]]' silent OMZP::ssh-agent
# PLUGINS ============================================================================================================ #
zi-turbo '0b' for \
  binary sbin'bin/*' \
    z-shell/nb \
  binary from"gh-r" sbin \
    ajeetdsouza/zoxide \
  has'lsd' atinit'AUTOCD=1' \
    z-shell/zsh-lsd \
    MichaelAquilina/zsh-you-should-use

export _ZO_CMD_PREFIX=''
zi ice  has'zoxide' ver'31-bug-incorrect-regex-matching-for-_zo_cmd_prefix-when-empty'
zi light z-shell/zsh-zoxide

zi load ss-o/zsh-functions

# **
# 亂 勇 勉 勒 勞 卑 墨 屢 履 廒廬 復 拉 敏 暑 易 擄 摒 僚 寮 尿 料 樂 燎 療 蓼 遼 龍 暈 阮 劉 杻 柳 流 溜 璘 藺 隣 鱗 麟 林 淋 臨 立 笠 粒 狀 炙 識 什 茶 
# 直 痢 異 留 琢 甆 煉 煮 烙 爵 爛 漣 漢 輸 賓 輦 蓮 連 鍊 列 劣 咽 烈 裂 說 廉 念 捻 殮 簾 獵 令 囹 寧 嶺 怜 玲 瑩 羚 聆 鈴 零 靈 領 例 禮 醴 隸 惡 了  
# 琉 留 硫 紐 類 六 戮 陸 倫 崙 淪 輪 律 慄 栗 率 隆 利 吏 履 易 李 梨 泥 理 痢 罹 裏 裡 里 離 匿 溺 吝 燐 刺 切 度 拓 糖 宅 洞 暴 輻 行 降 見 廓 兀 嗀 﨎  
# 塚 﨑 晴 﨓 﨔 凞 猪 益 礼 神 祥 福 靖 精 羽 﨟 蘒 﨡 諸 﨣 﨤 逸 都 﨧 﨨 﨩 飯 飼 館 鶴 郞 隷 侮 僧 免 勉 勤 卑 喝 嘆 器 塀 墨 層 屮 悔 慨 憎 﨏
# **

source "${HOME}/.dotfiles/sync/zsh/themes/p10k-robbyrussell.zsh"
